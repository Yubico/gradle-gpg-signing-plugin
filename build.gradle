buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.11'
  }
}
plugins {
  id 'java-gradle-plugin'
  id 'com.github.kt3k.coveralls' version '2.8.1'
}
apply plugin: 'groovy'
apply plugin: 'info.solidsoft.pitest'

repositories {
  mavenCentral()
}

sourceSets {
  integTest
}

configurations {
  integTestCompile.extendsFrom testCompile
  integTestRuntime.extendsFrom testRuntime
}

dependencies {

  testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
    exclude group: 'org.codehaus.groovy'
  }

  integTestCompile(
    sourceSets.main.output,
  )

}

group = 'com.yubico.gradle'
version = '0.1.0-alpha1'

task wrapper(type: Wrapper) {
  gradleVersion = '4.5'
}

task integTest(type: Test) {
  testClassesDirs = sourceSets.integTest.output.classesDirs
  classpath = sourceSets.integTest.runtimeClasspath

  shouldRunAfter test

  reports.html.destination = file("${reporting.baseDir}/${name}")
}
check.dependsOn integTest

pitest {
  testSourceSets = [ sourceSets.test, sourceSets.integTest ]
  timestampedReports = false
  outputFormats = ['XML', 'HTML']
}

coveralls {
  sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
}
tasks.coveralls {
  inputs.files tasks.pitest.outputs.files
}
